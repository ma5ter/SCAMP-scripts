; common TOS sizes
b equ 0
w equ 1
dw equ 2

; setting on is 0 for relay on usual state, 1 - on inversion

;duration equ 5 ; (param) duration of relay action after switch
;inversion equ 0 ; (param) inversion of relay action

pb macro ; byte
 psh (($1 >> 4) & 15)
 nib ($1 & 15)
endm

; push word macro
pw macro
 psh (($1 >> 12) & 15)
 nib (($1 >> 8) & 15)
 psh (($1 >> 4) & 15)
 nib ($1 & 15)
endm

; exp instruction with parameters
ex macro
 psh ($1 & 3)
 nib ((($3 & 1) << 3) | ($2 & 3))
 exp
endm

; BEGIN
 ; check last state considering inversion
 ; if was active - check timer
 ; else - check switch
 psh inversion
 xor b
 pop b
 skz
 bra check_switch ; was off
 ; was on, check_timer

check_timer
 dup dw ; save 'timer end time'
 psh TIMESTAMP
 inp b, dw
 ; compare 'current tme' and 'timer end time'
 ; if 'current time' greater - dispose all and set off
 ; else - check switch for off
 cmp dw
 skn
 bra check_switch_for_off
 pop dw
 bra set_off
 
check_switch_for_off
 ; read switch value
 pb HAS_SWITCH
 inp b, b
 pop b
 ; if has switch - set of
 ; else - continue, set on
 snz
 bra set_on
 pop dw
 bra set_off
 
check_switch
 ; read if has switch
 ; if has - start timer and set on
 ; else - set off
 pb HAS_SWITCH
 inp b, b
 pop b
 snz
 bra set_off ; wait
 ; start timer and switch on
 
start_timer
 psh TIMESTAMP
 inp b, dw
 pw duration
 ex w, dw, 1
 add dw ; calculate 'timer end time'

set_on
 psh inversion & 1
 ret
 
set_off
 psh ~inversion & 1
 ret